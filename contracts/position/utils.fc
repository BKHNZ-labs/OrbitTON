cell pack_position_code(int tick_lower, int tick_upper, slice owner, slice pool_address) inline {
  return begin_cell()
          .store_ref(begin_cell()
              .store_int(tick_lower, 24)
              .store_int(tick_upper, 24)
              .store_uint(0, 128)
              .store_uint(0, 256)
              .store_uint(0, 256)
              .end_cell())
          .store_ref(begin_cell()
              .store_uint(0, 128)
              .store_uint(0, 128)
              .store_slice(owner)
              .store_slice(pool_address)
              .end_cell())
        .end_cell();
}

cell calculate_position_state_init(int tick_lower, int tick_upper, slice owner, slice pool_address,  cell position_code) inline {
  return begin_cell()
    .store_uint(0, 2)
    .store_dict(position_code)
    .store_dict(pack_position_code(tick_lower, tick_upper, owner, pool_address))
    .store_uint(0, 1)
  .end_cell();
}

slice calculate_position_address(cell state_init) inline {
  return begin_cell().store_uint(4, 3)
    .store_int(WORKCHAIN, 8)
    .store_uint(cell_hash(state_init), 256)
  .end_cell().begin_parse();
}

(cell, slice) get_position_state_init_and_address(int tick_lower, int tick_upper, slice owner, slice pool_address, cell position_code) inline {
  cell state_init = calculate_position_state_init(tick_lower, tick_upper, owner, pool_address, position_code);
  return (state_init, calculate_position_address(state_init));
}
